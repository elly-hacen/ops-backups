apply plugin: 'com.android.application'

// Load local properties (contains debug keystore creds - never commit this file)
def localPropertiesFile = rootProject.file("local.properties")
def localProperties = new Properties()
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

android {
    namespace "com.termux.tasker"

    buildFeatures {
        buildConfig true
    }

    compileSdk project.properties.compileSdkVersion.toInteger()
    def appVersionName = System.getenv("TERMUX_TASKER_APP__BUILD__APP_VERSION_NAME") ?: ""
    def apkVersionTag = System.getenv("TERMUX_TASKER_APP__BUILD__APK_VERSION_TAG") ?: ""
    // Calculate version code from versionName: major*10000 + minor*100 + patch
    def baseVersionName = "2.1.1"
    def versionParts = baseVersionName.split("\\.")
    def defaultVersionCode = versionParts[0].toInteger() * 10000 + versionParts[1].toInteger() * 100 + versionParts[2].toInteger()
    def appVersionCode = System.getenv("TERMUX_TASKER_APP__BUILD__APP_VERSION_CODE")?.toInteger() ?: defaultVersionCode

    defaultConfig {
        applicationId "com.termux.tasker"
        minSdk project.properties.minSdkVersion.toInteger()
        targetSdk project.properties.targetSdkVersion.toInteger()
        versionCode appVersionCode
        versionName "2.1.1"

        if (appVersionName) versionName = appVersionName
        validateVersionName(versionName)
        
        buildConfigField "String", "VERSION_NAME", "\"${versionName}\""

        manifestPlaceholders.TERMUX_PACKAGE_NAME = "com.termux"
        manifestPlaceholders.TERMUX_APP_NAME = "Termux"
        manifestPlaceholders.TERMUX_TASKER_APP_NAME = "Termux:Tasker"
    }

    signingConfigs {
        debug {
            storeFile file(localProperties.getProperty('DEBUG_STORE_FILE', 'testkey_untrusted.jks'))
            keyAlias localProperties.getProperty('DEBUG_KEY_ALIAS', 'alias')
            storePassword localProperties.getProperty('DEBUG_STORE_PASSWORD', '')
            keyPassword localProperties.getProperty('DEBUG_KEY_PASSWORD', '')
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            zipAlignEnabled true
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = new File("ops-backups_" +
                    (apkVersionTag ? apkVersionTag : "v" + versionName + "+" + variant.buildType.name) + ".apk")
        }
    }

    packagingOptions {
        // Remove terminal-emulator and termux-shared JNI libs added via termux-shared dependency
        exclude "lib/*/libtermux.so"
        exclude "lib/*/liblocal-socket.so"
    }
}

dependencies {
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.1.4"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.2.1"
    androidTestImplementation "androidx.test:runner:1.6.2"
    androidTestImplementation "androidx.test:rules:1.6.1"

    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "androidx.annotation:annotation:1.9.1"
    implementation "com.google.android.material:material:1.12.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.work:work-runtime:2.9.0"
    implementation "com.google.guava:guava:24.1-jre"

    implementation "com.termux.termux-app:termux-shared:8aca6dbbf4"
    // Use if below libraries are published locally by termux-app with `./gradlew publishReleasePublicationToMavenLocal` and used with `mavenLocal()`.
    // If updates are done, republish there and sync project with gradle files here
    // https://github.com/termux/termux-app/wiki/Termux-Libraries
    //implementation "com.termux:termux-shared:0.118.0"

    implementation "com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava"

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

task versionName {
    doLast {
        print android.defaultConfig.versionName
    }
}

def validateVersionName(String versionName) {
    // https://semver.org/spec/v2.0.0.html#is-there-a-suggested-regular-expression-regex-to-check-a-semver-string
    // ^(0|[1-9]\d*)\.(0|[1-9]\d*)\.(0|[1-9]\d*)(?:-((?:0|[1-9]\d*|\d*[a-zA-Z-][0-9a-zA-Z-]*)(?:\.(?:0|[1-9]\d*|\d*[a-zA-Z-][0-9a-zA-Z-]*))*))?(?:\+([0-9a-zA-Z-]+(?:\.[0-9a-zA-Z-]+)*))?$
    if (!java.util.regex.Pattern.matches("^(0|[1-9]\\d*)\\.(0|[1-9]\\d*)\\.(0|[1-9]\\d*)(?:-((?:0|[1-9]\\d*|\\d*[a-zA-Z-][0-9a-zA-Z-]*)(?:\\.(?:0|[1-9]\\d*|\\d*[a-zA-Z-][0-9a-zA-Z-]*))*))?(?:\\+([0-9a-zA-Z-]+(?:\\.[0-9a-zA-Z-]+)*))?\$", versionName))
        throw new GradleException("The versionName '" + versionName + "' is not a valid version as per semantic version '2.0.0' spec in the format 'major.minor.patch(-prerelease)(+buildmetadata)'. https://semver.org/spec/v2.0.0.html.")
}
